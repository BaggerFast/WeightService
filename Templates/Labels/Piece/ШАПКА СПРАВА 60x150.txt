^XA ^CI28 ^LH0,0 ^FWB
{* 718 dots x 1782 dots *}

{* Полное наименование номенклатуры *}
^FO200,440 ^CF0,34,34 ^FB1320,3,0,J,0
^FD{{ plu_name }}^FS

{* Вложенность - Заголовок *}
^FO295,1505 ^CF0,26,26 ^FB250,1,0,L,0
^FDВложенность:^FS

{* Вложенность - Значение *}
^FO275,1395 ^CF0,59,59 ^FB200,1,0,L,0
^FD{{ bundle_count }} шт^FS

{* Замес *}
^FO295,1200 ^CF0,26,26 ^FB200,1,0,L,0
^FDЗамес: {{ kneading }}^FS

{* Цех/линия *}
^FO295,680 ^CF0,26,26 ^FB500,1,0,L,0
^FDЦех/Линия: {{ line_name }}^FS

{* ПЛУ *}
^FO295,480 ^CF0,26,26 ^FB200,1,0,L,0
^FDПЛУ: {{ plu_number }} / {{ line_number }}^FS

{* Дата изготовления - Заголовок *}
^FO350,1355 ^CF0,26,26 ^FB400,1,0,L,0
^FDДата изготовления:^FS

{* Дата изготовления - Значение *}
^FO335,1030 ^CF0,59,59 ^FB500,1,0,L,0
^FD{{ product_date }}^FS

{* Срок годности - Заголовок *}
^FO350,815 ^CF0,26,26 ^FB400,1,0,L,0
^FDГоден до:^FS

{* Срок годности - Значение *}
^FO335,595 ^CF0,59,59 ^FB500,1,0,L,0
^FD{{ expiration_date }}^FS

{* Изготовитель - Ресурсы шаблонов - MANUFACTURER_TITLE *}
^FO390,435 ^CF0,26,26 ^FB1320,1,0,J,0
^FD{{ manufacturer_title_sql }}^FS

{* Изготовитель - Адрес *}
^FO430,435 ^CF0,26,26 ^FB1320,1,0,J,0
^FD{{ line_address }}^FS

{* Описание номенклатуры*}
^FO470,435 ^CF0,26,26 ^FB1320,2,0,J,0
^FD{{ plu_description }}^FS

{* ШК сверху - Interleaved 2 of 5 Bar Code *}
^FT150,1755 ^BY3 ^B2B,120,Y,N,Y
^FD{{ barcode_top }}^FS

{* ШК снизу - GS1-128 *}
^FT630,1755 ^BY4 ^BCB,100,Y,N,N,D
^FD{{ barcode_bottom }}^FS

{* ШК справа - GS1-128 *}
^FO90,180 ^BY3 ^BCI,90,Y,Y,N,D
^FD{{ barcode_right }}^FS

{* Ресурсы шаблонов - EAC *}
^FO120,350
^FD{{ eac_sql }}^FS

{* Ресурсы шаблонов - Рюмка и вилка *}
^FO220,350
^FD{{ glass_with_fork_sql }}^FS

{* Ресурсы шаблонов - Переработка *}
^FO320,350
^FD{{ recycling_7_sql }}^FS

{* Ресурсы шаблонов - Скоропорт *}
^FO420,350
^FD{{ soon_to_be_spoiled_sql }}^FS

{* Ресурсы шаблонов - Градусник *}
^FO520,350
^FD{{ storage_method }}^FS

{* Линия ШАПКА *}
^FO 0,160 ^GB718,4,4
^FD^FS

{* Изготовитель ШАПКА *}
^FO 19,7 ^FWN ^CF0,33,33 ^FB680,1,0,C,0
^FD{{ manufacturer_title_sql }}\&^FS

{* Информация ШАПКА *}
^FO 19,45 ^FWN ^CF0,25,25 ^FB680,4,4,C,0
^FD{{ manufacturer_address_sql }}\&{{ manufacturer_phone_sql }}\&{{ manufacturer_site_sql }}\&^FS

^XZ